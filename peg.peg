# PE Grammar for PE Grammars
# 
# Adapted from [1] by Ian Piumarta <first-name at last-name point com>.
# 
# Best viewed using 140 columns monospaced with tabs every 8.
# 
# [1] Bryan Ford.  "Parsing Expression Grammars: A Recognition-Based Syntactic
#     Foundation."  Symposium on Principles of Programming Languages,
#     January 14--16, 2004, Venice, Italy.

package main

# parser declaration

type Peg Peg {
 *Tree
}

# Hierarchical syntax

Grammar		<- Spacing 'package' Spacing Identifier      { p.AddPackage(buffer[begin:end]) }
                           'type' Spacing Identifier         { p.AddPeg(buffer[begin:end]) }
                           'Peg' Spacing Action              { p.AddState(buffer[begin:end]) }
                           commit
                           Definition+ EndOfFile

Definition	<- Identifier 			{ p.AddRule(buffer[begin:end]) }
		     LEFTARROW Expression	{ p.AddExpression() } &(Identifier LEFTARROW / !.) commit
Expression	<- Sequence (SLASH Sequence	{ p.AddAlternate() }
			    )* (SLASH           { p.AddEmptyAlternate() }
                               )?
                 /
Sequence	<- Prefix (Prefix		{ p.AddSequence() }
			  )*
Prefix		<- AND Action			{ p.AddPredicate(buffer[begin:end]) }
		 / AND Suffix			{ p.AddPeekFor() }
		 / NOT Suffix			{ p.AddPeekNot() }
		 /     Suffix
Suffix          <- Primary (QUESTION            { p.AddQuery() }
                           / STAR               { p.AddStar() }
                           / PLUS               { p.AddPlus() }
                           )?
Primary	        <- 'commit' Spacing             { p.AddCommit() }
                 / Identifier !LEFTARROW        { p.AddName(buffer[begin:end]) }
                 / OPEN Expression CLOSE
                 / Literal                      { p.AddString(buffer[begin:end]) }
                 / Class                        { p.AddClass(buffer[begin:end]) }
                 / DOT                          { p.AddDot() }
                 / Action                       { p.AddAction(buffer[begin:end]) }
                 / BEGIN                        { p.AddBegin() }
                 / END                          { p.AddEnd() }

# Lexical syntax

Identifier	<- < IdentStart IdentCont* > Spacing
IdentStart	<- [a-zA-Z_]
IdentCont	<- IdentStart / [0-9]
Literal		<- ['] < (!['] Char )* > ['] Spacing
		 / ["] < (!["] Char )* > ["] Spacing
Class		<- '[' < (!']' Range)* > ']' Spacing
Range		<- Char '-' Char / Char
Char		<- '\\' [abefnrtv'"\[\]\\]
		 / '\\' [0-3][0-7][0-7]
		 / '\\' [0-7][0-7]?
		 / '\\' '-'
		 / !'\\' .
LEFTARROW	<- '<-' Spacing
SLASH		<- '/' Spacing
AND		<- '&' Spacing
NOT		<- '!' Spacing
QUESTION	<- '?' Spacing
STAR		<- '*' Spacing
PLUS		<- '+' Spacing
OPEN		<- '(' Spacing
CLOSE		<- ')' Spacing
DOT		<- '.' Spacing
Spacing		<- (Space / Comment)*
Comment		<- '#' (!EndOfLine .)* EndOfLine
Space		<- ' ' / '\t' / EndOfLine
EndOfLine	<- '\r\n' / '\n' / '\r'
EndOfFile	<- !.

Action		<- '{' < [^}]* > '}' Spacing
BEGIN		<- '<' Spacing
END		<- '>' Spacing
