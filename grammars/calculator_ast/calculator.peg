# Copyright 2010 The Go Authors. All rights reserved.
# Use of this source code is governed by a BSD-style
# license that can be found in the LICENSE file.

package main

type Calculator Peg {
}

e <- sp e1 !.
e1 <- e2 ( add e2
         / minus e2
         )*
e2 <- e3 ( multiply e3
         / divide e3
         / modulus e3
         )*
e3 <- e4 ( exponentiation e4
         )*
e4 <- minus value
    / value
value <- number
       / sub
number <- < [0-9]+ > sp
sub <- open e1 close
add <- '+' sp
minus <- '-' sp
multiply <- '*' sp
divide <- '/' sp
modulus <- '%' sp
exponentiation <- '^' sp
open <- '(' sp
close <- ')' sp
sp <- ( ' ' / '\t' )*
